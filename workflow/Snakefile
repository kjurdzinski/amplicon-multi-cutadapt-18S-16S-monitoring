import os
from snakemake.utils import validate
from scripts.common import glob_samples, read_sample_list, shortest_primer, longest_primer

# this container defines the underlying OS for each job when using the workflow
# with --use-conda --use-singularity
singularity: "docker://continuumio/miniconda3:4.9.2"

if os.path.exists("config/config.yaml"):
        configfile: "config/config.yaml"

# Set default config file
# if "--configfile" in config:
#     configfile: config["--configfile"]
# else:
#     if os.path.exists("config/config.yaml"):
#         configfile: "config/config.yaml"

# validate config
validate(config, "schemas/config.schema.yaml", set_default=True)

# define output folder from config file

# Read samples from file or glob from data dir
if os.path.exists(config["sample_list"]):
    samples=read_sample_list(config["sample_list"])
else:
    samples=glob_samples(config["data_dir"])

if len(samples) == 0:
    sys.exit(f"""
####################
ERROR: No input data.
Make sure that 'data_dir' (or its subdirectories) contains fastq files ending in
'R1*.fastq.gz' and 'R2*.fastq.gz'.
Your configured 'data_dir' is {config["data_dir"]}/
####################
""")

# make sure TMPDIR is set
try:
    os.environ["TMPDIR"]
except KeyError:
    os.environ["TMPDIR"] = "temp"
    os.makedirs(os.environ["TMPDIR"], exist_ok=True)

# calculate read length after trimming
config["cutadapt"]["1"] = config["cutadapt"]["expected_read_length"] - len(longest_primer(config["primers"]["forward"]))
config["cutadapt"]["2"] = config["cutadapt"]["expected_read_length"] - len(longest_primer(config["primers"]["reverse"]))

# calculate minimum read length as the shortest of the R1/R2 trimmed lengths
config["cutadapt"]["minimum_length"] = min(config["cutadapt"]["1"], config["cutadapt"]["2"])

localrules: all, rename_logs, multiqc, output_samples
ruleorder: cutadapt4 > rename_logs

rule all:
    input:
        expand("results/cutadapt/{sample}_trim_s4_R{i}{suff}",
            sample=samples.keys(), i=[1,2], suff=[".fastq.gz", "_fastqc.zip"])

rule qc_report:
    input:
        expand("results/multiqc_{s}.html",
            s=[1, 2, 3, 4])

rule trim:
    input:
        expand("results/cutadapt/{sample}_trim_s{s}_R{i}{suff}",
            sample=samples.keys(),i=[1, 2],s=[1, 2, 3],
            suff=[".fastq.gz", "_fastqc.zip"])

rule zip:
    input:
        "{f}.fastq"
    output:
        "{f}.fastq.gz"
    shell:
        """
        gzip -c {input} > {output}
        """

rule output_samples:
    """
    Generates a sample file list from the data dir path
    """
    output:
        "samples/{n}.tsv"
    run:
        with open(output[0], 'w') as fhout:
            fhout.write("sample\tR1\tR1_type\tR1_exists\tR2\tR2_type\tR2_exists\n")
            for sample in samples.keys():
                R1 = samples[sample]["R1"]
                R1_type = str(type(R1))
                exists_R1 = "yes" if os.path.exists(R1) or os.path.exists(R1.rstrip(".gz")) else "no"
                R2 = samples[sample]["R2"]
                R2_type = str(type(R2))
                exists_R2 = "yes" if os.path.exists(R2) or os.path.exists(R2.rstrip(".gz")) else "no"
                fhout.write("\t".join([sample, R1, R1_type, exists_R1, R2, R2_type, exists_R2]))
                fhout.write("\n")

rule cutadapt1:
    """
    Remove any reads containing adapters
    """
    input:
        R1=lambda wildcards: samples[wildcards.sample]["R1"],
        R2=lambda wildcards: samples[wildcards.sample]["R2"]
    output:
        R1=temp("results/cutadapt/{sample}_trim_s1_R1.fastq.gz"),
        R2=temp("results/cutadapt/{sample}_trim_s1_R2.fastq.gz")
    log: "logs/cutadapt/{sample}_trim_s1_R2.log"
    message: "Step 1 for {wildcards.sample}: Removing reads with adapters"
    threads: config["cutadapt"]["threads"]
    conda: "envs/cutadapt.yml"
    params:
        tmp1="$TMPDIR/{sample}_trim_s1_R1.fastq.gz",
        tmp2="$TMPDIR/{sample}_trim_s1_R2.fastq.gz",
        overlap=12,
        minimum_length=config["cutadapt"]["minimum_length"],
        adapter_F=config["cutadapt"]["adapter_forward"],
        adapter_R=config["cutadapt"]["adapter_reverse"]
    resources:
        runtime=lambda wildcards, attempt: attempt ** 2 * 30
    shell:
        """
        cutadapt --discard-trimmed -b {params.adapter_F} -B {params.adapter_R} -j {threads} \
            -o {params.tmp1} -p {params.tmp2} -O {params.overlap} \
            -m {params.minimum_length} {input.R1} {input.R2} >{log} 2>&1
        mv {params.tmp1} {output.R1}
        mv {params.tmp2} {output.R2}
        """

rule cutadapt2:
    """
    Only keep reads that contain any of the phasing primers at the start of 
    the 5' and remove the primers
    """
    input:
        R1="results/cutadapt/{sample}_trim_s1_R1.fastq.gz",
        R2="results/cutadapt/{sample}_trim_s1_R2.fastq.gz"
    output:
        R1=temp("results/cutadapt/{sample}_trim_s2_R1.fastq.gz"),
        R2=temp("results/cutadapt/{sample}_trim_s2_R2.fastq.gz")
    log: "logs/cutadapt/{sample}_trim_s2_R2.log"
    message: "Step 2 for {wildcards.sample}: Keeping reads with primers at 5'"
    threads: config["cutadapt"]["threads"]
    params:
        fw_adap=" ".join([f"-g ^{g}" for g in config["primers"]["forward"]]),
        rv_adap=" ".join([f"-G ^{G}" for G in config["primers"]["reverse"]]),
        tmp1="$TMPDIR/{sample}_trim_s2_R1.fastq.gz",
        tmp2="$TMPDIR/{sample}_trim_s2_R2.fastq.gz",
        minimum_length=config["cutadapt"]["minimum_length"]
    conda: "envs/cutadapt.yml"
    resources:
        runtime=lambda wildcards, attempt: attempt ** 2 * 30
    shell:
        """
        cutadapt --match-read-wildcards --discard-untrimmed --no-indels -e 0 {params.fw_adap} \
            {params.rv_adap} -j {threads} -o {params.tmp1} -p {params.tmp2} \
            -m {params.minimum_length} {input.R1} {input.R2} >{log} 2>&1
        mv {params.tmp1} {output.R1}
        mv {params.tmp2} {output.R2}
        """

rule cutadapt3:
    """
    Remove reads with primers anywhere else on the read
    """
    input:
        R1="results/cutadapt/{sample}_trim_s2_R1.fastq.gz",
        R2="results/cutadapt/{sample}_trim_s2_R2.fastq.gz"
    output:
        R1=temp("results/cutadapt/{sample}_trim_s3_R1.fastq.gz"),
        R2=temp("results/cutadapt/{sample}_trim_s3_R2.fastq.gz")
    log: "logs/cutadapt/{sample}_trim_s3_R2.log"
    message: "Step 3 for {wildcards.sample}: Remove reads with primers anywhere else"
    threads: config["cutadapt"]["threads"]
    conda: "envs/cutadapt.yml"
    params:
        shortest_fw=shortest_primer(config["primers"]["forward"]),
        shortest_rv=shortest_primer(config["primers"]["reverse"]),
        tmp1="$TMPDIR/{sample}_trim_s3_R1.fastq.gz",
        tmp2="$TMPDIR/{sample}_trim_s3_R2.fastq.gz",
        overlap=min(len(shortest_primer(config["primers"]["forward"])),
                    len(shortest_primer(config["primers"]["reverse"]))),
        minimum_length=config["cutadapt"]["minimum_length"]
    resources:
        runtime = lambda wildcards, attempt: attempt ** 2 * 30
    shell:
        """
        cutadapt --discard-trimmed -j {threads} -b {params.shortest_fw} \
            -B {params.shortest_rv} -o {params.tmp1} -p {params.tmp2} -O {params.overlap} \
            -m {params.minimum_length} {input.R1} {input.R2} > {log} 2>&1
        mv {params.tmp1} {output.R1}
        mv {params.tmp2} {output.R2}        
        """

rule cutadapt4:
    input:
        "results/cutadapt/{sample}_trim_s3_R{i}.fastq.gz"
    output:
        "results/cutadapt/{sample}_trim_s4_R{i}.fastq.gz"
    log:
        "logs/cutadapt/{sample}_trim_s4_R{i}.log"
    message: "Step 4 for {wildcards.sample}_{wildcards.i}: Trim reads to fixed length"
    threads: config["cutadapt"]["threads"]
    conda: "envs/cutadapt.yml"
    params:
        l=lambda wildcards: config["cutadapt"][wildcards.i],
        tmp="$TMPDIR/{sample}_trim_s4_R{i}.fastq.gz"
    resources:
        runtime=lambda wildcards, attempt: attempt ** 2 * 30
    shell:
        """
        cutadapt -j {threads} --length {params.l} -o {params.tmp} \
            {input} > {log} 2>&1
        mv {params.tmp} {output}
        """

rule rename_logs:
    input:
        "logs/cutadapt/{sample}_trim_s{s}_R2.log"
    output:
        "logs/cutadapt/{sample}_trim_s{s}_R1.log"
    shell:
        """
        cat {input} | sed 's/_R2/_R1/g' > {output}
        """

rule fastqc:
    input:
        "{prefix}.fastq.gz"
    output:
        "{prefix}_fastqc.html",
        "{prefix}_fastqc.zip"
    log: "logs/fastqc/"+os.path.basename("{prefix}")+".log"
    message: "Fastqc on {wildcards.prefix}"
    conda: "envs/fastqc.yml"
    resources:
        runtime = lambda wildcards, attempt: attempt ** 2 * 60 * 1
    threads: 4
    shell:
        """
        fastqc -q -t {threads} --noextract {input} > {log} 2>&1
        """

rule multiqc:
    input:
        fastqc=expand("results/cutadapt/{sample}_trim_s{{s}}_R{i}_fastqc.zip",
            sample=samples.keys(), i=[1, 2]),
        logs=expand("logs/cutadapt/{sample}_trim_s{{s}}_R{i}.log",
            sample=samples.keys(), i=[1,2]),
    output:
        "results/multiqc_{s}.html"
    log: "logs/multiqc/multiqc_{s}.log"
    message: "Generating MultiQC report for step {wildcards.s}"
    params:
        outdir=lambda wildcards, output: os.path.dirname(output[0]),
        filelist="multiqc_{s}.files",
        n="multiqc_{s}.html",
        title="MultiQC report for cutadapt step {s}"
    shell:
        """
        echo {input} | tr ' ' '\n' > {params.filelist}
        multiqc -f -n {params.n} -o {params.outdir} -l {params.filelist} \
            --title "{params.title}" \
            --cl_config "{{'extra_fn_clean_exts': [{{'type': 'regex', 'pattern': '_trim_s[1,2,3,4]'}}, {{'type': 'regex', 'pattern': '_\d+.fastq.gz'}}]}}" > {log} 2>&1
        rm -f {params.filelist}
        """
